apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = '1.8'

mainClassName = 'com.page5of4.todo.data.Main'

configurations {
    capsule
}

run {
    systemProperty "dropwizard.config", "build/resources/main/todo-data.yml"
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.7.0'

    compile 'com.squareup.dagger:dagger:1.2.1'
    compile 'com.squareup.dagger:dagger-compiler:1.2.1'

    compile 'com.netflix.feign:feign-core:6.1.2'
    compile 'com.netflix.feign:feign-jaxrs:6.1.2'
    compile 'com.netflix.feign:feign-jackson:6.1.2'

    compile 'io.dropwizard:dropwizard-db:0.7.0'
    compile 'io.dropwizard:dropwizard-jdbi:0.7.0'
    compile 'org.jooq:jooq:3.3.2'
    runtime 'com.h2database:h2:1.4.178'

    compile "com.google.guava:guava:14.0.1"
    compile "com.netflix.karyon:karyon-core:1.0.19"
    compile "com.netflix.karyon:karyon-extensions:1.0.19"
    compile "com.netflix.archaius:archaius-core:0.5.11"
    compile "com.netflix.eureka:eureka-core:1.1.132"
    compile "com.netflix.eureka:eureka-client:1.1.132"
    compile 'org.slf4j:log4j-over-slf4j:1.6.1'

    capsule 'co.paralleluniverse:capsule:0.4.0'

    compile 'com.yammer.tenacity:tenacity-core:0.3.0'

    compile 'com.page5of4.dropwizard:dropwizard-karyon:0.1.0-SNAPSHOT'

    testCompile 'junit:junit:4.11'
}

task capsule(type: Jar, dependsOn: classes) {
    archiveName = "todo-data.jar"

    from jar
    from { configurations.runtime }

    from(configurations.capsule.collect { zipTree(it) }) { include 'Capsule.class' }

    manifest {
        attributes(
                'Main-Class': 'Capsule',
                'Application-Class': mainClassName,
                'Application-Version': version,
                'Min-Java-Version': '1.8.0',
                'JVM-Args': run.jvmArgs.join(' '),
                'System-Properties': run.systemProperties.collect { k, v -> "$k=$v" }.join(' '),
        )
    }
}

